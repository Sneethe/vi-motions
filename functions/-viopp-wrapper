local key
read -k 1 key
if [[ $key = 's' ]]; then
	case ${WIDGET#zvmm-} in
		vi-change*) zle change-surround -w ;;
		vi-delete*) zle delete-surround -w ;;
		vi-yank*)   zle add-surround -w ;;
	esac
else

	# Ref zshzle:
	# zle -U:
	# - After the widget currently executed finishes(!)
	# - ZLE will behave as if the characters of $key were typed by the user
	# So by wrapping zle -U in a widget, make sure that $key is ready for us
	# within the context of THIS widget
	zle zvmm-push-string $key

	# change binding for push-string call, then change back ASAP
	bindkey -M "$KEYMAP" "$KEYS" "${WIDGET#zvmm-}"
	sched +0 bindkey -M "$KEYMAP" "$KEYS" "${WIDGET}"
	zle ${WIDGET#zvmm-} -w
fi


# vim: syntax=zsh
